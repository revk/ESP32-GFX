#!/bin/csh -f
# Bodgy script to make fonts... Use the fonts I commit, probably easier
setenv d `pwd`
if(! -e include) then
	echo run in main dir
	exit 1
endif
if(! -e /tmp/5x9) then
	/projects/tools/bin/5x9 --style=JTD
	cd /tmp/5x9
	foreach u (u00[0-7]*.svg)
		echo $u
		inkscape $u --verb EditSelectAll --verb SelectionUnGroup --verb SelectionUnion --verb FileSave --verb FileQuit
		inkscape -z --export-area=0:102:600:1024 -o $u:r.png $u >& /dev/null
	end
endif
cd /tmp/5x9
foreach n (1 2 3 4 5)
	@ W = $n * 6
	@ H = $n * 9
	@ b = $W * $H / 2
	set f="grey$n.h"
	echo "$f"
	echo "static const uint8_t gfx_font${n}[]={ // $W/$H ($b bytes per character)" > $f
	foreach u (u00[0-7]*.svg)
		convert $u:r.png -flatten -negate -scale ${W}x${H}'!' char.gray
		echo "// $u:r" >> $f
		od -v -tx1 -w$W -Anone char.gray | sed -e 's/\([0-9a-f]\)[0-9a-f] \([0-9a-f]\)[0-9a-f]/0x\1\2,/g' >> $f
	end
	echo "};" >> $f
	mv -f "$f" "$d/include"
	@ b = ( $W + 7 ) / 8 * $H
	set f="mono$n.h"
	echo "$f"
	echo "static const uint8_t gfx_font${n}[]={ // $W/$H ($b bytes per character)" > $f
	foreach u (u00[0-7]*.svg)
		convert $u:r.png -flatten -negate -scale ${W}x${H}'!' char.mono
		echo "// $u:r" >> $f
		od -v -tx1 -w$W -Anone char.mono | sed -e 's/\([0-9a-f][0-9a-f]\)/0x\1,/g' >> $f
	end
	echo "};" >> $f
	mv -f "$f" "$d/include"
end
sed "$d/include/grey0.h" \
	    -e 's/0x00,0x00, /0x00, /g' \
	    -e 's/0x00,0xF0, /0x01, /g' \
	    -e 's/0x0F,0x00, /0x02, /g' \
	    -e 's/0x0F,0xF0, /0x03, /g' \
	    -e 's/0xF0,0x00, /0x04, /g' \
	    -e 's/0xF0,0xF0, /0x05, /g' \
	    -e 's/0xFF,0x00, /0x06, /g' \
	    -e 's/0xFF,0xF0, /0x07, /g' \
	    > mono0.h
mv -f mono0.h "$d/include"
