#!/bin/csh -f
# Bodgy script to make fonts... Use the fonts I commit, probably easier
setenv d `pwd`
if(! -e include) then
	echo run in main dir
	exit 1
endif
if(! -e /tmp/5x9JTDBold) then
	/projects/tools/bin/5x9 --style=JTD --dir=/tmp/5x9JTDBold --bold
	cd /tmp/5x9JTDBold
	foreach u (u00[0-7]*.svg)
		echo $u
		inkscape --export-area=0:0:612:918 -o $u:r.png $u >& /dev/null
	end
endif
if(! -e /tmp/5x9JTD) then
	/projects/tools/bin/5x9 --style=JTD --dir=/tmp/5x9JTD
	cd /tmp/5x9JTD
	foreach u (u00[0-7]*.svg)
		echo $u
		inkscape --export-area=0:0:612:918 -o $u:r.png $u >& /dev/null
	end
endif
if(! -e /tmp/5x9LCD) then
	/projects/tools/bin/5x9 --style=LCD --dir=/tmp/5x9LCD --bold
	cd /tmp/5x9LCD
	foreach u (u00[0-7]*.svg)
		echo $u
		inkscape --export-area=50:50:662:968 -o $u:r.png $u >& /dev/null
	end
endif
if(! -e /tmp/7seg) then
	mkdir /tmp/7seg
	cd /tmp/7seg
	foreach s (a b c d e f g h i)
		echo $s
		inkscape --actions="export-id:$s;export-id-only;export-area-page;export-background:white;export-filename:$s.png;export-do" $d/SevenSeg.svg >& /dev/null
	end
endif
foreach n (1 2 3 4 5 6 7 8 9)
	if($n <= 6) then
		cd /tmp/5x9JTD
		if($n == 1) cd /tmp/5x9JTDBold
		@ W = $n * 6
		@ H = $n * 9
		@ b = $W * $H / 2
		set f="grey$n.h"
		echo "$f"
		echo "static const uint8_t gfx_font${n}[]={ // $W/$H ($b bytes per character)" > $f
		foreach u (u00[0-7]*.svg)
			convert $u:r.png -flatten -negate -scale ${W}x${H}'!' char.gray
			echo "// $u:r" >> $f
			od -v -tx1 -w$W -Anone char.gray | sed -e 's/\([0-9a-f]\)[0-9a-f] \([0-9a-f]\)[0-9a-f]/0x\1\2,/g' >> $f
		end
		echo "};" >> $f
		mv -f "$f" "$d/include"
		if($n == 1) cd /tmp/5x9LCD
		@ b = ( $W + 7 ) / 8 * $H
		set f="mono$n.h"
		echo "$f"
		echo "static const uint8_t gfx_font${n}[]={ // $W/$H ($b bytes per character)" > $f
		foreach u (u00[0-7]*.svg)
			convert $u:r.png -flatten -monochrome -dither None -scale ${W}x${H}'!' char.mono
			echo "// $u:r" >> $f
			od -v -tx1 -w$W -Anone char.mono | sed -e 's/\([0-9a-f][0-9a-f]\)/0x\1,/g' >> $f
		end
		echo "};" >> $f
		mv -f "$f" "$d/include"
	endif
	@ W = $n * 7
	@ H = $n * 9
	cd /tmp/7seg
	set f="7seg$n.h"
	echo "$f"
	echo "static const uint8_t gfx_7seg${n}[]={ // $W/$H ($b bytes per character)" > $f
	foreach s (a b c d e f g h i)
		convert $s:r.png -flatten -monochrome -dither None -scale ${W}x${H}'!' char.mono
		echo "// $s:r" >> $f
		od -v -tx1 -w$W -Anone char.mono | sed -e 's/\([0-9a-f][0-9a-f]\)/0x\1,/g' >> $f
	end
	echo "};" >> $f
	mv -f "$f" "$d/include"
end
sed "$d/include/grey0.h" \
	    -e 's/0x00,0x00, /0x00, /g' \
	    -e 's/0x00,0xF0, /0x04, /g' \
	    -e 's/0x0F,0x00, /0x02, /g' \
	    -e 's/0x0F,0xF0, /0x06, /g' \
	    -e 's/0xF0,0x00, /0x01, /g' \
	    -e 's/0xF0,0xF0, /0x05, /g' \
	    -e 's/0xFF,0x00, /0x03, /g' \
	    -e 's/0xFF,0xF0, /0x07, /g' \
	    > mono0.h
mv -f mono0.h "$d/include"
