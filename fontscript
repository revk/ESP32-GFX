#!/bin/csh -f
# Bodgy script to make fonts... Use the fonts I commit, probably easier
make fontpack
setenv d `pwd`
if(! -e include) then
	echo run in main dir
	exit 1
endif
if(! -e /tmp/5x9JTDBold) then
	/projects/tools/bin/5x9 --style=JTD --dir=/tmp/5x9JTDBold --bold
	cd /tmp/5x9JTDBold
	foreach u (u00[0-7]*.svg)
		echo $u
		inkscape --export-area=0:0:612:918 -o $u:r.png $u >& /dev/null
	end
endif
if(! -e /tmp/5x9JTD) then
	/projects/tools/bin/5x9 --style=JTD --dir=/tmp/5x9JTD
	cd /tmp/5x9JTD
	foreach u (u00[0-7]*.svg)
		echo $u
		inkscape --export-area=0:0:612:918 -o $u:r.png $u >& /dev/null
	end
endif
if(! -e /tmp/5x9LCD) then
	/projects/tools/bin/5x9 --style=LCD --dir=/tmp/5x9LCD --bold
	cd /tmp/5x9LCD
	foreach u (u00[0-7]*.svg)
		echo $u
		inkscape --export-area=50:50:662:968 -o $u:r.png $u >& /dev/null
	end
endif
if(! -e /tmp/7seg) then
	mkdir /tmp/7seg
	cd /tmp/7seg
	foreach s (a b c d e f g h i)
		echo $s
		inkscape --actions="export-id:$s;export-id-only;export-area-page;export-background:white;export-filename:$s.png;export-do" $d/SevenSeg.svg >& /dev/null
	end
endif
foreach n (1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20)
	if($n <= 6) then
		cd /tmp/5x9JTD
		if($n == 1) cd /tmp/5x9JTDBold
		@ W = $n * 6
		@ H = $n * 9
		@ b = $W * $H / 2
		set f="grey$n.h"
		echo "$f"
		foreach u (u00[0-7]*.svg)
			convert $u:r.png -flatten -negate -scale ${W}x${H}'!' $u:r.gray
		end

		$d/fontpack -w$W -h$H -b4 u00[0-7]*.gray --head="gfx_font$n" >> "$f"
		mv -f "$f" "$d/include"

		$d/fontpack -w$W -h$H -b4 u00[0-7]*.gray --pack --head="gfx_font_pack$n" > "pack$f"
		mv -f "pack$f" "$d/include"

		if($n == 1) cd /tmp/5x9LCD
		@ b = ( $W + 7 ) / 8 * $H
		set f="mono$n.h"
		echo "$f"
		foreach u (u00[0-7]*.svg)
			convert $u:r.png -flatten -monochrome -dither None -scale ${W}x${H}'!' $u:r.mono
		end

		$d/fontpack -w$W -h$H -b1 u00[0-7]*.mono --head="gfx_font$n" > "$f"
		mv -f "$f" "$d/include"

		$d/fontpack -w$W -h$H -b1 u00[0-7]*.mono --pack --head="gfx_font_pack$n" > "pack$f"
		mv -f "pack$f" "$d/include"
	endif
	@ W = $n * 7
	@ H = $n * 9
	cd /tmp/7seg
	set f="7seg$n.h"
	echo "$f"
	foreach s ([a-i].png)
		convert $s:r.png -flatten -monochrome -dither None -scale ${W}x${H}'!' $s:r.mono
	end

	$d/fontpack -w$W -h$H -b1 [a-i].mono --head="gfx_7seg$n" > "$f"
	mv -f "$f" "$d/include"

	$d/fontpack -w$W -h$H -b1 [a-i].mono --pack --head="gfx_7seg_pack$n" > "pack$f"
	mv -f "pack$f" "$d/include"
end
sed "$d/include/grey0.h" \
	    -e 's/0x00,0x00, /0x00,/g' \
	    -e 's/0x00,0xF0, /0x04,/g' \
	    -e 's/0x0F,0x00, /0x02,/g' \
	    -e 's/0x0F,0xF0, /0x06,/g' \
	    -e 's/0xF0,0x00, /0x01,/g' \
	    -e 's/0xF0,0xF0, /0x05,/g' \
	    -e 's/0xFF,0x00, /0x03,/g' \
	    -e 's/0xFF,0xF0, /0x07,/g' \
	    > mono0.h
mv -f mono0.h "$d/include"
